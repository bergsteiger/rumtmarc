USES
 WordsTuning.script
 WordsTranslation.script
 F1ControlsDefinition.script
;

: "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Control
 focused:control:push =: l_Control
 l_Control ЯВЛЯЕТСЯ class::TevCustomEditorWindow 
  [[ 'Фокус не в редакторе, а в ' l_Control pop:component:Name ':' l_Control pop:object:ClassName ]] strings:Cat 
   ASSERTS
;

: "Перейти на параграф вверх"
 "Убедиться, что фокус в редакторе"
 "Перейти на параграф вверх в редакторе {(focused:control:push)}"
; 

: "Перейти на параграф вниз"
 "Убедиться, что фокус в редакторе"
 "Перейти на параграф вниз в редакторе {(focused:control:push)}"
; 

VAR g_EtalonCount
[ 0 >>> g_EtalonCount ]

: "Сравнить текст редактора с эталоном" OBJECT IN anEditor
 ++! g_EtalonCount
 OnTest
 VAR l_File
 ( g_EtalonCount РАВНО 1 ) IF
  script:FileName '.evd' sysutils:ChangeFileExt
 ELSE
  script:FileName '.' g_EtalonCount IntToStr Cat '.evd' Cat sysutils:ChangeFileExt
 ENDIF
 sysutils:ExtractFileName >>> l_File
 l_File anEditor pop:editor:TextToFile
 l_File '%' tests:CheckEtalon
;

: "Сравнить текст с эталоном"
 "Сравнить текст редактора {(DocumentText:push)} с эталоном"
;

: "Сравнить абзац редактора с эталоном" OBJECT IN anEditor
 anEditor pop:editor:CurrentText #10 '[разрыв строки]' string:Replace .
;

: "Сравнить абзац текущего редактора с эталоном"
 "Убедиться, что фокус в редакторе"
 "Сравнить абзац редактора {(focused:control:push)} с эталоном"
; 

: "Сравнить выделенный текст редактора с эталоном" OBJECT IN anEditor
 STRING VAR "Выделенный текст"
 "Убедиться, что фокус в редакторе"
 anEditor pop:editor:GetSelectionText "Заменить непечатаемые символы" =: "Выделенный текст"
 "Выделенный текст" .
; // "Сравнить выделенный текст редактора с эталоном"

: "Сравнить выделенный текст текущего редактора с эталоном"
 "Убедиться, что фокус в редакторе"
 "Сравнить выделенный текст редактора {(focused:control:push)} с эталоном"
; // "Сравнить выделенный текст текущего редактора с эталоном"

: "Сравнить выделенный текст редактора с эталоном в формате" OBJECT IN anEditor STRING INTEGER IN aFormat
 cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 aFormat anEditor pop:editor:GetSelectionTextInFormat .
;

: "Сравнить выделенный текст текущего редактора с эталоном в формате" STRING INTEGER IN aFormat
 "Убедиться, что фокус в редакторе"
 "Сравнить выделенный текст редактора {(focused:control:push)} с эталоном в формате {(aFormat)}"
;

: "Проверяем отсутствие комментария в редакторе" OBJECT IN anEditor
 anEditor pop:editor:HasComment ! 'Комментарий удалить не удалось' ASSERTS
;

: "Проверяем отсутствие комментария в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Проверяем отсутствие комментария в редакторе {(focused:control:push)}" 
;

: "Удалить текущий комментарий и проверить что он удалился"
 "Убедиться, что фокус в редакторе"
 focused:control:push pop:editor:DeleteComment 'Не удалось удалить комментарий' ASSERTS
 "Проверяем отсутствие комментария в текущем редакторе"
;

: "Проверяем наличие комментария в редакторе" OBJECT IN anEditor
 anEditor pop:editor:HasComment 'Комментарий поставить не удалось' ASSERTS
;

: "Проверяем наличие комментария в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Проверяем наличие комментария в редакторе {(focused:control:push)}"
;

: "Выделить таблицу в текущем редакторе"
 "Выделить таблицу в редакторе {(focused:control:push)}"
;

: "Выделить весь текст в текущем редакторе"
 "Убедиться, что фокус в редакторе"
 "Выделить всё в редакторе {(focused:control:push)}"
;

: "Установить в редакторе указатель мыши на точку для вызова КМ" OBJECT IN Editor INTEGER IN X0 INTEGER IN Y0 
 VAR x
 VAR y
 Editor pop:editor:CursorCoordsToScreen >>> y >>> x
 x X0 + >>> x
 y Y0 + >>> y
 x y Editor pop:control:ScreenToClient
;

: "Скроллируем вниз раз" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 aCount раз ( l_Editor pop:editor:ScrollLineDown
 "Дать системе перерисоваться" )
;

: "Скроллируем вверх раз" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 aCount раз ( l_Editor pop:editor:ScrollLineUp
 "Дать системе перерисоваться" )
;

: "Выделить параграфов" INTEGER IN aCount
 "Убедиться, что фокус в редакторе"
 OBJECT VAR l_Editor
 focused:control:push >>> l_Editor 
 l_Editor pop:editor:ParaHome
 // - чтобы выделять с начала параграфа, иначе эталоны будут нестабильны
 cc:StartSelBlock
 "{(aCount)} раз {(@ "Перейти на параграф вниз")}"
 l_Editor pop:editor:ParaEnd
 // - чтобы выделять ДО конца параграфа, иначе эталоны будут нестабильны
 cc:EndSelBlock
;

: "Сравнить с эталоном следующий параграф"
 "Перейти на параграф вниз"
 "Выделить {(1)} параграфов"
 "Сравнить выделенный текст текущего редактора с эталоном"
; // "Сравнить с эталоном следующий параграф"

USES
 EVDSchema.script
;

 : "Убедиться, что фокус в таблице"
  "Убедиться, что фокус в редакторе"
  evd::id_Table focused:control:push pop:editor:PushParaFromCursor 3 раза ( Para:GetParent ) Para:Type:Inherits 'Курсор не в таблице' ASSERTS
 ; // "Убедиться, что фокус в таблице"

: "Вставить строку в текущую таблицу"
 "Убедиться, что фокус в таблице"
 "Вставить строку в таблицу"
 ; // "Вставить строку таблицы"

: "Текущий выделенный текст"
 focused:control:push pop:editor:CurrentText
; // "Текущий выделенный текст"

: "Сравнить с эталоном левый отступ до края формы текущего редактора"
 "Убедиться, что фокус в редакторе"
 "Контрол в фокусе" "Левый отступ контрола" .
; // "Сравнить с эталоном левый отступ до края формы текущего редактора"

: "Ищем строку в эталоне документа" STRING IN aStr
 STRING VAR Etalon
 cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 CF_RTF "Контрол в фокусе" "Взять выделенный текст для сравнения в определенном формате"  >>> Etalon
 Etalon aStr "Определить позицию найденной строки в эталоне" БОЛЬШЕ 0 IF
  (  [[ ' Строка: ' aStr ' найдена. ' ]] strings:Cat .
  EXIT )
 ELSE
  (  [[ ' Строка: ' aStr ' не найдена. ' ]] strings:Cat . )
 ENDIF
; // "Ищем строку в эталоне документа"

: "Узнать номер текущего параграфа"
 "Убедиться, что фокус в редакторе"
 evd::ti_Handle "Контрол в фокусе" pop:editor:PushParaFromCursor Para:IntA
; // "Узнать номер текущего параграфа"

: "Ищем строку в эталоне HTML" STRING IN aStr
 STRING VAR Etalon
 cc:Copy
 // - затычка, иначе не будет правильно имя формата в код формата преобразовываться
 CF_HTML "Контрол в фокусе" "Взять выделенный текст для сравнения в определенном формате"  >>> Etalon
 Etalon aStr "Определить позицию найденной строки в эталоне" БОЛЬШЕ 0 IF
  (  [[ ' Строка: ' aStr ' найдена. ' ]] strings:Cat .
  EXIT )
 ELSE
  (  [[ ' Строка: ' aStr ' не найдена. ' ]] strings:Cat . )
 ENDIF
; // "Ищем строку в эталоне HTML"